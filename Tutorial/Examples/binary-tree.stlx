map := procedure() {
    return @Nil();
};

insert := procedure(m, k1) {
    match (m) {
        case @Nil() : 
             return @Node(k1, @Nil(), @Nil());
        case @Node(k2, l, r) | k1 == k2: 
             return @Node(k1, l, r);
        case @Node(k2, l, r) | compare(k1, k2) < 0:
             return @Node(k2, insert(l, k1), r);
        case @Node(k2, l, r):
             return @Node(k2, l, insert(r, k1));
        default: abort("Error in insert($m$, $k1$)");
    }
};

toString := procedure(m, n) {
    match (m) {
        case @Nil() :
             return " " * n + "Nil";
        case @Node(k, l, r) :
             return toString(l, n + 4) + "\n" + 
                    " " * n + "<" + k + ">\n" +
                    toString(r, n + 4);
        default: abort("toString($m$, $n$)");
    }
};
    
demo := procedure() {
    m := map();
    m := insert(m, "anton" );
    m := insert(m, "hugo"  );
    m := insert(m, "gustav");
    m := insert(m, "jens"  );
    m := insert(m, "hubert");
    m := insert(m, "andre" );
    m := insert(m, "rene"  );
    print("\n$toString(m, 0)$\n");
};

demo();



