// this program demonstrates the parser and eval functionality

getInput := procedure() {
    getValue := procedure() {
        try {
            return eval(get("Please enter some SetlX value: "));
        } catch (error) {
            print(error);
            print();
            print("Oh... you did that wrong... try again!");
            print();
            return getValue();
        }
    };

    value := getValue();
    print("value\t:= $value$");
    print();

    getExpr := procedure() {
        try {
            expr:= parse(get("Please enter some SetlX expression using 'value' as variable: "));
            if (! isTerm(expr) || "@@@variable(\"value\")" notin canonical(expr)) {
                throw("wrong!");
            }
            return expr;
        } catch (error) {
            print(error);
            print();
            print("Oh... you did that wrong... try again!");
            print();
            return getExpr();
        }
    };

    expr := getExpr();
    print("expr\t:= $expr$");
    print("Syntax-Tree:\nexpr\t:= " + canonical(expr));
    print();

    try {
        return evalTerm(expr);
    } catch (error) {
        print();
        print("No, No, No... that did not work out!");
        print("Try again from the beginning, but don't make this error again:\n$error$");
        print();
        return getInput();
    }
};

result := getInput();
print("result\t:= $result$");
print();

print("Great result!");

